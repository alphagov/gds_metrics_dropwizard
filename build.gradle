plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

repositories {
    mavenCentral()
}

apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group = 'engineering.gds-reliability'

description = ' Library for prometheus instrumentation in Dropwizard based apps.'

sourceCompatibility = 1.11
targetCompatibility = 1.11

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    configurations = ['archives']
    publish = true

    pkg {
        repo = 'maven'
        name = 'gds-metrics-dropwizard'
        userOrg = 'alphagov'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/alphagov/gds_metrics_dropwizard.git'
    }
}

test {
    dependsOn << compileTestJava
    testLogging {
        events "skipped", "failed", "standardError"
        showCauses true
        showExceptions true
        showStackTraces true
        exceptionFormat "full"

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }

    baseName = 'gds-metrics-dropwizard'
}

ext {
    dropwizard_version = '1.3.27'
    mockito_version = '3.7.7'
}

dependencies {
    api 'io.prometheus:simpleclient_dropwizard:0.9.0'
    api 'io.prometheus:simpleclient_servlet:0.9.0'
    api 'io.prometheus:simpleclient_hotspot:0.9.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.glassfish.jersey.core:jersey-client:2.25.1'
    implementation'javax.servlet:javax.servlet-api:4.0.1'

    compileOnly "io.dropwizard:dropwizard-core:$dropwizard_version"

    testImplementation 'junit:junit:4.13.1'
    testImplementation "org.mockito:mockito-core:$mockito_version"
    testImplementation "io.dropwizard:dropwizard-testing:$dropwizard_version"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

// from https://mark.koli.ch/fail-build-on-java-compiler-warnings
tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:all" << "-Xlint:-processing" << "-Xlint:-serial" << "-Werror"
}
